//import com.github.jengelman.gradle.plugins.shadow.transformers.ManifestResourceTransformer
//import com.github.jengelman.gradle.plugins.shadow.transformers.ServicesFileTransformer
//import com.github.jengelman.gradle.plugins.shadow.transformers.ApacheLicenseResourceTransformer
//import com.github.jengelman.gradle.plugins.shadow.transformers.ApacheNoticeResourceTransformer

plugins {
    //id "com.github.johnrengelman.shadow" version "2.0.4"
    id "application"
    id "maven-publish"
    id "signing"
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = "io.github.galbiston.geosparql_fuseki.Main"
}
mainClassName = "io.github.galbiston.geosparql_fuseki.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    ext.slf4jVersion = "1.7.25"
    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    ext.jenaVersion = "3.9.0"
    //implementation "org.apache.jena:apache-jena-libs:$jenaVersion"
    //Issue resolving apache-jena-libs in Maven projects. Use individual modules as provide jars.
    implementation "org.apache.jena:jena-core:$jenaVersion"
    implementation "org.apache.jena:jena-arq:$jenaVersion"
    implementation "org.apache.jena:jena-iri:$jenaVersion"
    implementation "org.apache.jena:jena-tdb:$jenaVersion"

    implementation (group: 'org.apache.jena', name: 'jena-fuseki-server', version: jenaVersion){
        exclude group: "org.apache.jena", module: "apache-jena-libs"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    ext.jcommanderVersion = "1.72"
    implementation "com.beust:jcommander:$jcommanderVersion"

    ext.logbackVersion = "1.2.3"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"

    ext.junitVersion = "4.12"
    testImplementation "junit:junit:$junitVersion"

    ext.commonsCollectionsVersion = "4.2"
    implementation "org.apache.commons:commons-collections4:$commonsCollectionsVersion"

    //GeoSPARQL Extension
    ext.geosparqlVersion = "1.0.1"
    implementation "io.github.galbiston:geosparql-jena:$geosparqlVersion"

    //Apache SIS EPSG database - allows full range of EPSG codes but download and licensing implications.
    //Use of EPSG data is incomptabile for bundling with Apache licence, see: http://sis.apache.org/epsg.html
    ext.sisVersion = "0.8"
    testImplementation "org.apache.sis.non-free:sis-embedded-data:$sisVersion"
}

group = 'io.github.galbiston'
version = '1.0.1'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                artifactId = 'geosparql-fuseki'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = 'GeoSPARQL Jena'
                    description = 'API implemenation of GeoSPARQL 1.0 standard using Apache Jena.'
                    url = 'https://github.com/galbiston/geosparql-fuseki'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'Greg Albiston'
                            email = 'galbiston@mail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/galbiston/geosparql-fuseki.git'
                        developerConnection = 'scm:git:ssh://git@github.com:galbiston/geosparql-fuseki.git'
                        url = 'https://github.com/galbiston/geosparql-jena.git'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

build.finalizedBy(publishToMavenLocal)

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'io.github.galbiston.geosparql_fuseki.Main'
    }
}

/*
shadowJar {
transform(com.github.jengelman.gradle.plugins.shadow.transformers.ManifestResourceTransformer)
transform(com.github.jengelman.gradle.plugins.shadow.transformers.ServicesFileTransformer)
transform(com.github.jengelman.gradle.plugins.shadow.transformers.ApacheLicenseResourceTransformer)
transform(com.github.jengelman.gradle.plugins.shadow.transformers.ApacheNoticeResourceTransformer)
}
 */
